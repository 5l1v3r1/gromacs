#!/bin/csh -f

if ( $?cwd ) then
  setenv	CUR	$cwd
else
  setenv	CUR	`pwd`
endif
setenv 	CORE	gmxcore
setenv	BNC	gmxbench
setenv	TST	gmxtests

clear
if ( -d src ) then
    echo        'Source already present'
    echo        'Will only configure GMXRC'
else
    echo	'Starting GROMACS installation'
    echo	'-----------------------------'

    echo 	'Unpacking archives...'

    foreach fz ( $CORE $BNC $TST )
    echo 	$fz
    if ( -f $fz.taz ) then
	    cat $fz.taz | uncompress -c | tar xf -
    else
	    if ( -f $fz.tgz ) then
		    cat $fz.tgz | gzip -d -c | tar xf -
	    else
		    if ( $fz == $CORE ) then
			    echo FATAL, no file $fz.tgz or $fz.taz found
			    exit
		    endif
	    endif
    endif
    end
endif

echo ""

# Determine which CPUS are available
setenv CPUS ""
set ncpu = 0
foreach i ( src/makef/Makefile.* )
  setenv	EXT	$i:e
  if ( ( $EXT != SGI ) && ( $EXT != def ) && ( $EXT != std ) && ( $EXT != no ) && ( $EXT != ruggr2 ) && ( $EXT != ins ) && ( $EXT != ind) ) then
     setenv	CPUS	"$CPUS"" ""$EXT"
     @ ncpu += 1
  endif
end

echo	'                   Which CPU/Machine do you have ?'
echo	'--------------------------------------------------------------------'
set i = 0
foreach cpu ( $CPUS )
  set desc=`head -1 src/makef/Makefile.$cpu`
  if ( "$desc" == "#" ) then
    set desc="# Unknown"
  endif
  printf "%5s - %-26s" $cpu "$desc"
  if ( $i == 0 ) then
    set i = 1
  else
    printf "\n"
    set i = 0
  endif
end
echo ""

setenv	MKPREFIX	src/makef/Makefile

echo	'Please select the cpu that is appropriate'
set 	cpu = $<
if ( -f $MKPREFIX.$cpu ) then
	echo 	$cpu is OK
	set  	cpmf = 0
else
	echo 	'Give a short abbreviation for your CPU/Machine, eg. ibm'
	set	cpu = $<
	set	cpmf = 1
endif

if ( $cpmf == 1 ) then
	cp	$MKPREFIX.no	$MKPREFIX.$cpu
	echo	'I have created a file called ' $MKPREFIX.$cpu
	echo	'Please follow the instructions in the online readme.hmtl file'
endif

echo	' '
echo	'GROMACS Cool Quote Installation'
echo	'-------------------------------'
echo	'If you do *NOT* want cool quotes type No here'
set	cq = $<
if ( $cq != No ) then
	setenv 	COOL	YES
else
	setenv	COOL	NO
	echo	'Now you will be treated like a fast food addict'
endif

setenv	RC	 GMXRC
echo	Creating $RC file
if ( -f  $RC ) mv $RC $RC.bak

setenv	PATHPREFIX	`pwd`
cat	> $RC << EOD
#
# Directories, edit to match your site, we assume
# users have a c-shell allways
#

# remove previous GROMACS environment, if present
source $PATHPREFIX/NOGMX -quiet

# This is were the sources are
setenv GMXHOME	$PATHPREFIX

#
# If you support multiple machines then it's useful to 
# have a switch statement here, which, depending on hostname
# point to the proper directories, and sets the GMXCPU variable
# 
# For easy updating, it is also recommended to put you local
# stuff in the GMXRC.local file
# Don't forget the GMXCPU variable
#
setenv	LOCAL_RC	\$GMXHOME/GMXRC.local
if ( -f \$LOCAL_RC ) then
	source	\$LOCAL_RC
else
# Here are some defaults, if you have one machine, edit these
	setenv	GMXCPU	$cpu
endif

# Some directories that live below the root
setenv GMXBIN 	\$GMXHOME/bin/\$GMXCPU
setenv GMXLIB	\$GMXHOME/top

# 
# Default Graphics Font
#
setenv GMXFONT	10x20

#
# Set the path #
setenv 	PATH 	"\$PATH":"\$GMXBIN"
#

if ( \$?LD_LIBRARY_PATH ) then
	 setenv LD_LIBRARY_PATH	"\$LD_LIBRARY_PATH":\$GMXHOME/lib/\$GMXCPU
else
	 setenv LD_LIBRARY_PATH	\$GMXHOME/lib/\$GMXCPU
endif
if ( \$?MANPATH ) then
	setenv 	MANPATH		"\$MANPATH":\$GMXHOME/man
else
	setenv  MANPATH         /usr/man:\$GMXHOME/man  
endif

# Now finally, if you consider our Cool Quotes to be offensive
# you might consider commenting out the following line:
setenv	IAMCOOL	$COOL

if ( -f \$GMXHOME/complete ) source \$GMXHOME/complete

# end of script
EOD

echo	'Done, it is compile time now.'
echo	'Source GMXRC and read instructions in'
echo	'http://md.chem.rug.nl/~gmx/readme2.0.html'
