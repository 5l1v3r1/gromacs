# remove all environment variables set by GMXRC

setenv bVerb 1
if ( "$1" == "-quiet" ) setenv bVerb 0

if ($bVerb) echo "Being loud and noisy"

if ( $shell == "/bin/tcsh" ) then
  if ( $?GMXBIN ) then
    if ( -d $GMXBIN ) then
      if ( $bVerb ) echo "uncomplete all GROMACS programs"
      foreach f ( `\ls $GMXBIN` )
        uncomplete $f
      end
    endif
  endif
endif

if ( ($?LD_LIBRARY_PATH) && ($?GMXHOME) && (($?GMXCPU) || ($?CPU)) ) then
  if ( (! $?GMXCPU) && ($?CPU) ) then
    setenv GMXCPU $CPU
  endif
  unsetenv TEMPPATH
  unsetenv PATHSET
  foreach i ( `echo $LD_LIBRARY_PATH | sed "s/:/ /g"` )
    setenv PATHSET
    if ( ( "$i"  != "$GMXHOME/lib/$GMXCPU" ) && ( "$i/" != "$GMXHOME/lib/$GMXCPU" ) ) then
      if ( ! $?TEMPPATH ) then
	setenv TEMPPATH $i
      else
	setenv TEMPPATH "$TEMPPATH":"$i"
      endif
    else
      if ( $bVerb ) echo "Removed $GMXHOME/lib/$GMXCPU from LD_LIBRARY_PATH"
    endif
  end
  if ($?PATHSET) then
    if (! $?TEMPPATH) setenv TEMPPATH
    setenv LD_LIBRARY_PATH $TEMPPATH
    unsetenv TEMPPATH
    unsetenv PATHSET
  else
    echo "You might want to remove $GMXHOME/lib/$GMXCPU from LD_LIBRARY_PATH"
  endif
endif

if ($?GMXCPU) then
  if ( $bVerb ) echo "unsetenv GMXCPU ($GMXCPU)"
  unsetenv GMXCPU
endif

if ($?GMXLIB) then
  if ( $bVerb ) echo "unsetenv GMXLIB ($GMXLIB)"
  unsetenv GMXLIB 
endif

if ( ($?GMXMAN) && ($?MANPATH) ) then
  if ($MANPATH == $GMXMAN) then
    unsetenv MANPATH
  else
    if ( $bVerb ) echo "You might want to remove $GMXMAN ("\$"GMXMAN) from "\$"MANPATH"
  endif
endif

if ($?GMXMAN) then
  if ( $bVerb ) echo "unsetenv GMXMAN ($GMXMAN)"
  unsetenv GMXMAN
endif

if ($?GMXHOME) then
  if ( $bVerb ) echo "unsetenv GMXHOME ($GMXHOME)"
  unsetenv GMXHOME
endif

if ($?GMXROOT) then
  if ( $bVerb ) echo "unsetenv GMXROOT ($GMXROOT)"
  unsetenv GMXROOT
endif

if ( ($?GMXBIN) && ($?PATH) ) then
  foreach i ( $path )
    if ( ( "$i" != "$GMXBIN" ) && ( "$i/" != "$GMXBIN" ) ) then
      if ( ! $?TEMPPATH ) then
	setenv TEMPPATH $i
      else
	setenv TEMPPATH "$TEMPPATH":"$i"
      endif
    else
      if ( $bVerb ) echo Removed $GMXBIN from path
    endif
  end
  if (! $?TEMPPATH) setenv TEMPPATH
  setenv PATH $TEMPPATH
  unsetenv TEMPPATH
endif

if ($?GMXBIN) then
  if ( $bVerb ) echo "unsetenv GMXBIN ($GMXBIN)"
  unsetenv GMXBIN
endif
if ($?VER) then
  if ( $bVerb ) echo "unsetenv VER ($VER)"
  unsetenv VER
endif
if ($?GMXVER) then
  if ( $bVerb ) echo "unsetenv GMXVER ($GMXVER)"
  unsetenv GMXVER
endif
if ($?GMXNPRI) then
  if ( $bVerb ) echo "unsetenv GMXNPRI ($GMXNPRI)"
  unsetenv GMXNPRI
endif
if ($?GMXFONT) then
  if ( $bVerb ) echo "unsetenv GMXFONT ($GMXFONT)"
  unsetenv GMXFONT
endif
if ($?AMBBIN) then
  if ( $bVerb ) echo "unsetenv AMBBIN ($AMBBIN)"
  unsetenv AMBBIN
endif
if ($?IAMCOOL) then
  if ( $bVerb ) echo "unsetenv IAMCOOL ($IAMCOOL)"
  unsetenv IAMCOOL
endif

#last line
