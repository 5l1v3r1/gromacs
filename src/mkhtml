#!/bin/csh -f

setenv VER 	1.6
setenv HTML	$GMXHOME/html
if ( ! -d $HTML ) mkdir $HTML
setenv GIFDIR   $HTML/gif
if ( -f $GIFDIR/rainbow.gif ) then
  setenv HR	'<IMG SRC="gif/rainbow.gif" WIDTH=100% HEIGTH=7 >'
else
  setenv HR	'<HR>'
endif
setenv HTMLOL   $HTML/online$VER
if ( ! -d $HTMLOL ) mkdir $HTMLOL
setenv	INDEX	$HTML/online_index$VER.html
if ( -f $INDEX ) \rm $INDEX

cat > $INDEX << EOD
<html>
<h1>Index</h1>
$HR
<h3>General</h3>
<ul>
<li><A HREF="changes$VER.html" TARGET="bottom" >New features</a>
<p>
<li><A HREF="online$VER/getting_started.html" TARGET="bottom" >Getting Started</a>
<p>
<li><A HREF="online$VER/flow.html" TARGET="right" >Flow chart</a>
<p>
<li><A HREF="online$VER/files.html" TARGET="right" >File formats</a>
<p>
<li><A HREF="online$VER/mdp_opt.html" TARGET="right" >mdp options</a>
<p>
<li><A HREF="online$VER/count.html" TARGET="right" >Top of the pops</a>
</ul>
$HR
<h3>Programs</h3>
<ul>
<li><A HREF="online$VER/general.html" TARGET="right" >General</a>
<p>
EOD

cd 	$GMXHOME/bin/$CPU
foreach i ( * )
  if ( ( -x $i ) && ( $i != "my_dssp" ) ) then
    echo -n "$i "
    $i -quiet -man 2 >& /dev/null
    if ( -f $i.html ) then 
      if ( -f $HTMLOL/$i.html ) \rm $HTMLOL/$i.html
      mv $i.html $HTMLOL
      cat >> $INDEX << EOK
<li><a href="online$VER/$i.html" TARGET="right">$i</a>
EOK
    endif
  endif
end

cat >> $INDEX << EOM
</ul>
$HR
</HTML>
EOM

echo "New html pages in $HTML (file:/$INDEX)"

#last line
